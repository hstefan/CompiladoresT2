// a, b : int;
// in : char;
//
// x : auto = 30;
//
// input a, b, c;
//
// if a > b then
//     i := b;
// end;
//
// output a, b, max;

@@skip := "(?:\s*(?://[^\n]*\n)?)*" ;
@identifier := "([a-zA-Z_][a-zA-Z0-9_]*)" ;
@int-literal := "([0-9]+)" ;
@real-literal := "([0-9]+\.[0-9]+)" ;
@char-literal := "'(.)'" ;
@string-literal := "\"([^\"]*)\"" ;

identifier-list := @identifier => ( ',' => @identifier )* ;

generic := '[' => type-expression => ']' ;
basic-type := 'int' | 'real' | 'char' | 'bool' | 'string' | 'list' => generic | 'map' => generic => generic;
type-decoration := '&' | ( '[' => @int-literal => ']' ) ;
type-expression := basic-type => type-decoration* ;

bool-literal := 'true' | 'false' ;
list-literal := '[' => expression-list? => ']' ;
map-entry := expression => ':' => expression ;
map-entry-list := map-entry => ( ',' => map-entry )* ;
map-literal := '{' => map-entry-list? => '}' ;

value := @identifier | @real-literal | @char-literal | bool-literal | @string-literal | list-literal | map-literal ;
expr-e := value | '(' => expression => ')' ;
expr-d := 'not'? => expr-e => l-value-decoration* ;
expr-c := expr-d => ( ( '*' | '/' | '%' ) => expr-d )* ;
expr-b := expr-c => ( ( '+' | '-' ) => expr-c )* ;
relational-op := '==' | '!=' | '<' | '<=' | '>' | '>=' ;
expr-a := expr-b => ( relational-op => expr-b )* ;
expression := expr-a => ( ( 'and' | 'or' ) => expr-a )* ;
expression-list := expression => ( ',' => expression )* ;

l-value-decoration := '&' | ( '[' => expression => ']' ) ;
l-value := @identifier => l-value-decoration* ;
l-value-list := l-value => ( ',' => l-value )* ;

input-statement := 'input' => l-value-list ;
output-statement := 'output' => expression-list ;
if-statement := 'if' => expression => 'then' => statement* => ( 'else' => statement* )? => 'end' ;
while-statement := 'while' => expression => 'do' => statement* => 'end' ;
assignment := l-value => ':=' => expression ;
declaration := 'var' => identifier-list => ':' => type-expression => ( ':=' => expression )? ;

statement := ( input-statement | output-statement | if-statement | while-statement | assignment | declaration ) => ';' ;
#program := statement* ;
